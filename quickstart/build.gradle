apply plugin: 'com.android.application'
apply plugin: 'com.github.dcendents.android-maven'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'kotlin-kapt'

android {
    compileSdkVersion 27

    defaultConfig {
        applicationId "jp.shiguredo.sora.quickstart"
        minSdkVersion 16
        targetSdkVersion 26
        versionCode 1
        versionName "1.0"

        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"

        // アプリで参照する設定を BuildConfig に書き込む。
        // gradle.properties に endpoint/channel 設定がある場合は上書きする
        def endpoint   = findProperty("sora.signaling_endpoint") ?: signaling_endpoint
        def channel_id = findProperty("sora.channel_id")         ?: channel_id
        buildConfigField("String", "SIGNALING_ENDPOINT", "\"${endpoint}\"")
        buildConfigField("String", "CHANNEL_ID",         "\"${channel_id}\"")
    }

    sourceSets {
        main {
            java.srcDirs += 'src/main/kotlin'
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
}


dependencies {
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:${kotlin_version}"
    implementation "org.jetbrains.kotlin:kotlin-reflect:${kotlin_version}"

    ext.support_version = '27.0.2'
    implementation "com.android.support:appcompat-v7:${support_version}"
    implementation "com.android.support:design:${support_version}"

    // Anko libs
    ext.anko_version = '0.10.5'
    implementation "org.jetbrains.anko:anko-sdk15:${anko_version}"
    implementation "org.jetbrains.anko:anko-appcompat-v7:${anko_version}"
    implementation "org.jetbrains.anko:anko-sdk15-listeners:${anko_version}"

    ext.permissionsdispatcher_version = '3.3.1'
    implementation ("com.github.hotchemi:permissionsdispatcher:${permissionsdispatcher_version}") {
        exclude module: "support-v13"
    }
    kapt "com.github.hotchemi:permissionsdispatcher-processor:${permissionsdispatcher_version}"

    // Sora Android SDK
    implementation("com.github.shiguredo:sora-android-sdk:${sora_android_sdk_version}@aar") {
        transitive = true
    }
}

configurations.all {
    resolutionStrategy {
        cacheDynamicVersionsFor 0, 'seconds'
        cacheChangingModulesFor 0, 'seconds'
    }
}

tasks.withType(org.jetbrains.kotlin.gradle.tasks.KotlinCompile).all {
    kotlinOptions {
        jvmTarget = "1.8"
    }
}
