apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-kapt'
apply plugin: 'org.jlleitschuh.gradle.ktlint'

android {
    compileSdkVersion 36

    defaultConfig {
        applicationId "jp.shiguredo.sora.quickstart"
        minSdkVersion 26
        targetSdkVersion 36
        versionCode 1
        versionName "1.0"

        // アプリで参照する設定を BuildConfig / resource に書き込む。
        buildConfigField("String", "SIGNALING_ENDPOINT", "\"${signaling_endpoint}\"")
        buildConfigField("String", "CHANNEL_ID",         "\"${channel_id}\"")
        buildConfigField("String", "SIGNALING_METADATA", "\"${signaling_metadata}\"")

        manifestPlaceholders = [usesCleartextTraffic: usesCleartextTraffic]
    }

    sourceSets {
        main {
            java.srcDirs += 'src/main/kotlin'
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    buildFeatures {
        // AGP 8.0 からデフォルトで false になった
        // このオプションが true でないと、defaultConfig に含まれている
        // buildConfigField オプションが無効になってしまうため、true に設定する
        // 参考: https://developer.android.com/build/releases/past-releases/agp-8-0-0-release-notes#default-changes
        buildConfig true
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    viewBinding {
        enabled = true
    }
    // AGP 8.0 からモジュールレベルの build script 内に namespace が必要になった
    // 参考: https://developer.android.com/build/releases/past-releases/agp-8-0-0-release-notes#namespace-dsl
    namespace 'jp.shiguredo.sora.quickstart'
}

ktlint {
    version = "0.45.2"
    android = false
    outputToConsole = true
    reporters {
        reporter "checkstyle"
    }
    ignoreFailures = false
}

dependencies {
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:${kotlin_version}"

    implementation 'com.google.code.gson:gson:2.13.1'

    implementation 'androidx.appcompat:appcompat:1.7.0'
    implementation 'com.google.android.material:material:1.12.0'

    implementation "com.github.permissions-dispatcher:permissionsdispatcher:4.9.2"
    kapt "com.github.permissions-dispatcher:permissionsdispatcher-processor:4.9.2"

    // Sora Android SDK
    if (findProject(':sora-android-sdk') != null) {
        // module is included
        api project(':sora-android-sdk')
    } else {
        // external dependency
        implementation("com.github.shiguredo:sora-android-sdk:${sora_android_sdk_version}@aar") {
            transitive = true
        }
    }
}

configurations.all {
    resolutionStrategy {
        cacheDynamicVersionsFor 0, 'seconds'
        cacheChangingModulesFor 0, 'seconds'
    }
}

tasks.withType(org.jetbrains.kotlin.gradle.tasks.KotlinCompile).all {
    finalizedBy("ktlintFormat")
    kotlinOptions {
        jvmTarget = "1.8"
    }
}
