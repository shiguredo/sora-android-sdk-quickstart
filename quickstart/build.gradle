apply plugin: 'com.android.application'
apply plugin: 'com.github.dcendents.android-maven'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'kotlin-kapt'

android {
    compileSdkVersion 28

    defaultConfig {
        applicationId "jp.shiguredo.sora.quickstart"
        minSdkVersion 21
        targetSdkVersion 28
        versionCode 1
        versionName "1.0"

        // アプリで参照する設定を BuildConfig / resource に書き込む。
        buildConfigField("String", "SIGNALING_ENDPOINT", "\"${signaling_endpoint}\"")
        buildConfigField("String", "CHANNEL_ID",         "\"${channel_id}\"")

        manifestPlaceholders = [usesCleartextTraffic: usesCleartextTraffic];
    }

    sourceSets {
        main {
            java.srcDirs += 'src/main/kotlin'
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
}


dependencies {
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:${kotlin_version}"
    implementation "org.jetbrains.kotlin:kotlin-reflect:${kotlin_version}"

    implementation "androidx.appcompat:appcompat:1.0.2"
    implementation "com.google.android.material:material:1.0.0"

    ext.pd_version = '4.5.0'
    implementation "org.permissionsdispatcher:permissionsdispatcher:${pd_version}"
    kapt "org.permissionsdispatcher:permissionsdispatcher-processor:${pd_version}"

    // Sora Android SDK
    if (findProject(':sora-android-sdk') != null) {
        // module is included
        api project(':sora-android-sdk')
    } else {
        // external dependency
        implementation("com.github.shiguredo:sora-android-sdk:${sora_android_sdk_version}@aar") {
            transitive = true
        }
    }
}

configurations.all {
    resolutionStrategy {
        cacheDynamicVersionsFor 0, 'seconds'
        cacheChangingModulesFor 0, 'seconds'
    }
}

tasks.withType(org.jetbrains.kotlin.gradle.tasks.KotlinCompile).all {
    kotlinOptions {
        jvmTarget = "1.8"
    }
}
