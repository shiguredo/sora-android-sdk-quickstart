apply plugin: 'com.android.application'
apply plugin: 'com.github.dcendents.android-maven'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'kotlin-kapt'

android {
    compileSdkVersion 26

    defaultConfig {
        applicationId "jp.shiguredo.sora.quickstart"
        minSdkVersion 16
        targetSdkVersion 25
        versionCode 1
        versionName "1.0"

        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"

        // アプリで参照する設定を BuildConfig に書き込む。
        // gradle.properties に endpoint/channel 設定がある場合は上書きする
        def endpoint   = findProperty("sora.signaling_endpoint") ?: signaling_endpoint
        def channel_id = findProperty("sora.channel_id")         ?: channel_id
        buildConfigField("String", "SIGNALING_ENDPOINT", "\"${endpoint}\"")
        buildConfigField("String", "CHANNEL_ID",         "\"${channel_id}\"")
    }

    sourceSets {
        main {
            java.srcDirs += 'src/main/kotlin'
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
}


dependencies {
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jre7:${kotlin_version}"

    ext.support_version = '26.0.2'
    implementation "com.android.support:appcompat-v7:${support_version}"
    implementation "com.android.support:design:${support_version}"

    // Anko libs
    ext.anko_version = '0.9.1'
    implementation "org.jetbrains.anko:anko-sdk19:${anko_version}"
    implementation "org.jetbrains.anko:anko-appcompat-v7:${anko_version}"

    implementation ('com.github.hotchemi:permissionsdispatcher:3.1.0') {
        exclude module: "support-v13"
    }
    kapt 'com.github.hotchemi:permissionsdispatcher-processor:3.1.0'

    // Sora Android SDK
    implementation("com.github.shiguredo:sora-android-sdk:${sora_android_sdk_version}@aar") {
        transitive = true
    }
}

configurations.all {
    resolutionStrategy {
        cacheDynamicVersionsFor 0, 'seconds'
        cacheChangingModulesFor 0, 'seconds'
    }
}
